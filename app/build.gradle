apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    namespace 'com.iterable.iterableapi.testapp'
    testNamespace 'iterable.com.iterableapi'

    defaultConfig {
        applicationId "com.iterable.iterableapi.testapp"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            enableAndroidTestCoverage true
        }
    }

    testOptions.unitTests.includeAndroidResources = true

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.fragment:fragment:1.2.4'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    debugImplementation 'androidx.fragment:fragment-testing:1.2.4'

    implementation project(':iterableapi')
    implementation project(':iterableapi-ui')

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation 'org.robolectric:shadows-playservices:4.9.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.mockito:mockito-android:3.0.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoDebugTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group = "reporting"
    description = "Generate unified Jacoco code coverage report"
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
    def fileFilter = [
            '**/*Test*.*',
            '**/AutoValue_*.*',
            '**/*JavascriptBridge.class',
            '**/R.class',
            '**/R$*.class',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/BuildConfig.*',
            '**/*$ViewBinder*.*',
            '**/*$ViewInjector*.*',
            '**/Lambda$*.class',
            '**/Lambda.class',
            '**/*Lambda.class',
            '**/*Lambda*.class',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class',
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter) //we use "debug" build type for test coverage (can be other)
    def sdkTree = fileTree(dir: "${buildDir}/../../iterableapi/build/intermediates/javac/debug/classes", excludes: fileFilter)
    def sdkUiTree = fileTree(dir: "${buildDir}/../../iterableapi-ui/build/intermediates/javac/debug/classes", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"
    def sdkSrc = "${project.projectDir}/../iterableapi/src/main/java"
    def sdkUiSrc = "${project.projectDir}/../iterableapi-ui/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    additionalSourceDirs.from = files([sdkSrc, sdkUiSrc])
    additionalClassDirs.from = files([sdkTree, sdkUiTree])
    executionData.from = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec"
    ])
}

task jacocoDebugAndroidTestReport(type: JacocoReport, dependsOn: ['connectedCheck']) {
    group = "reporting"
    description = "Generate Jacoco code coverage report for instumentation tests"

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }

    def fileFilter = [
            '**/*Test*.*',
            '**/AutoValue_*.*',
            '**/*JavascriptBridge.class',
            '**/R.class',
            '**/R$*.class',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/BuildConfig.*',
            '**/*$ViewBinder*.*',
            '**/*$ViewInjector*.*',
            '**/Lambda$*.class',
            '**/Lambda.class',
            '**/*Lambda.class',
            '**/*Lambda*.class',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class',
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter) //we use "debug" build type for test coverage (can be other)
    def sdkTree = fileTree(dir: "${buildDir}/../../iterableapi/build/intermediates/javac/debug/classes", excludes: fileFilter)
    def sdkUiTree = fileTree(dir: "${buildDir}/../../iterableapi-ui/build/intermediates/javac/debug/classes", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"
    def sdkSrc = "${project.projectDir}/../iterableapi/src/main/java"
    def sdkUiSrc = "${project.projectDir}/../iterableapi-ui/src/main/java"
    
    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    additionalSourceDirs.from = files([sdkSrc, sdkUiSrc])
    additionalClassDirs.from = files([sdkTree, sdkUiTree])
    executionData.from = fileTree(dir: "$buildDir", include: "outputs/code_coverage/debugAndroidTest/connected/**/*.ec")
}