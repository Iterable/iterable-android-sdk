apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    namespace 'com.iterable.iterableapi'
    testNamespace 'iterable.com.iterableapi'

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27

        buildConfigField "String", "ITERABLE_SDK_VERSION", "\"3.5.3\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            enableAndroidTestCoverage true
            multiDexEnabled true
        }
    }

    testOptions.unitTests.all {
        testLogging {
            exceptionFormat "full"
        }
    }

    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.appcompat:appcompat:1.0.0'
    api 'androidx.annotation:annotation:1.0.0'
    api 'com.google.firebase:firebase-messaging:20.3.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation 'org.robolectric:shadows-playservices:4.9.2'
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation project(':iterableapi')

    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
    androidTestImplementation 'org.mockito:mockito-android:3.0.0'
}

ext {
    libraryName = 'iterableapi'
    libraryVersion = '3.5.3'
}

if (hasProperty("mavenPublishEnabled")) {
    apply from: '../maven-push.gradle'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    excludes = ['**/*.kt']
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    exclude '**/*.kt'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

// A hack to import the classpath and BuildConfig into the javadoc task
afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant -> variant.javaCompile.classpath.files })
    javadoc.classpath += files(android.libraryVariants.collect { variant -> "build/generated/source/r/${variant.flavorName}/${variant.buildType.name}" })
    javadoc.classpath += files(android.libraryVariants.collect { variant -> "build/generated/source/buildConfig/${variant.flavorName}/${variant.buildType.name}" })
}
